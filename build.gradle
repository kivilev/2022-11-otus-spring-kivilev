plugins {
    id 'java'
    id 'idea'
    id 'checkstyle'
    id 'com.github.spotbugs' version '5.0.9'
    id "io.freefair.lombok" version "6.6-rc1"
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "io.spring.dependency-management" version "1.1.0"
    id 'org.springframework.boot' version '2.7.6'
    id 'project-report'
}

idea {
    project {
        languageLevel = 11
    }
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

def mongockVersion = "4.3.8"
def springBootVersion = "2.7.6"

allprojects {
    group "org.kivilev"

    repositories {
        mavenLocal()
        mavenCentral()
    }

    apply plugin: "io.spring.dependency-management"
    dependencyManagement {
        dependencies {
            dependency 'org.springframework:spring-context:5.3.24'
            dependency 'org.junit.jupiter:junit-jupiter-api:5.9.0'
            dependency 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
            dependency 'com.opencsv:opencsv:5.7.1'
            dependency 'org.mockito:mockito-junit-jupiter:4.9.0'
            dependency "org.springframework.boot:spring-boot-starter:$springBootVersion"
            dependency "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
            dependency "org.springframework.boot:spring-boot-configuration-processor:$springBootVersion"
            dependency 'org.springframework.shell:spring-shell-starter:2.1.4'
            dependency "org.springframework.boot:spring-boot-starter-jdbc:$springBootVersion"
            dependency "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion"
            dependency 'com.h2database:h2:2.1.214'
            dependency 'org.apache.commons:commons-lang3:3.12.0'
            dependency "org.springframework.boot:spring-boot-starter-data-mongodb:$springBootVersion"
            dependency 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:3.4.11'
            dependency "com.github.cloudyrock.mongock:mongock-spring-v5:$mongockVersion"
            dependency "com.github.cloudyrock.mongock:mongodb-springdata-v3-driver:$mongockVersion"
        }
    }

    apply plugin: 'checkstyle'
    checkstyle {
        toolVersion '10.3.4'
        configFile file("$project.rootDir/checkstyle.xml")
        ignoreFailures = false
        maxWarnings = 0
        maxErrors = 0
    }

    apply plugin: 'com.github.spotbugs'
    spotbugs {
        showProgress = false
        effort = 'max'
        reportLevel = 'low'
        excludeFilter = file("$project.rootDir/spotbugs-exclude.xml")
    }

    apply plugin: 'project-report'
}

compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile).configureEach {
    options.with {
        encoding = 'UTF-8'
    }
}

bootJar {
    enabled = false
}